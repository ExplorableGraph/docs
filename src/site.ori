{
  # This file defines the top-level virtual values in the tree used to create the
  # Graph Origami documentation.

  # The first set of formulas define a pipeline for the documentation content,
  # which is authored as a tree of markdown files in the `content` folder.

  areas = @tree/plain(areas.yaml)

  # Add the `area` and `fileName` properties to the documents.
  withData = @tree/map({
    keyName: "area"
    valueFn: =@if(
      @tree/isAsyncTree(_)
      @tree/map({
        extensions: "md"
        valueFn: =js/addData.js(_, area, @key, areas, @tree/plain(content(area)/pages.yaml), client/assets/origami)
      })(_)
      =js/addData.js(_, "", area, areas, {}, client/assets/origami)
    )
  })(content)

  # Take the markdown files and inline any Graph Origami expressions contained
  # in curly braces.
  inlined = @tree/map({ deep: @true, extensions: "md", valueFn: @inline })(withData)

  # Convert the markdown (including inlined content) to HTML while preserving
  # the front matter at the top of each document. As the content is now HTML, we
  # change the file extension from `.md` to `.html`. The result is a tree of
  # HTML content; each value represents the main body of a given page.
  html = @tree/map({ deep: @true, extensions: "mdâ†’html", valueFn: @mdHtml })(inlined)

  # Apply the documentation page template. This injects the HTML content from
  # the previous step into the site's main page template, which adds headers and
  # navigation chrome. This step concludes the main documentation pipeline.
  pages = @tree/map({ deep: @true, extensions: "html", valueFn: templates/page.orit })(html)

  # The `public` tree defines the final tree which is built into static pages.
  public = @tree/merge(client, pages, {
    # This components.js "bundle" is just concatenated files
    components.js = @tree/concat(components)

    demos = {
      aboutUs = src/demos/aboutUs/site.ori/public
    }

    # Used by Netlify
    _redirects = src/_redirects

    samples = samples.ori/
  })
}
